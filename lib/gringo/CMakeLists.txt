file(GLOB LIBGRINGO_SRC src/*.cpp)

include_directories(${CMAKE_CURRENT_BINARY_DIR} ${CMAKE_CURRENT_BINARY_DIR}/config include generated ..)

MACRO(RE2C NAME SRC DST)
	IF(NOT EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/generated/${NAME}.cpp")
		# this macro requires re2c
		IF(NOT RE2C_FOUND)
			FIND_PACKAGE(RE2C REQUIRED)
		ENDIF(NOT RE2C_FOUND)
		ADD_CUSTOM_COMMAND(
			OUTPUT ${DST}/${NAME}.cpp
			COMMAND ${RE2C_EXECUTABLE} -o ${DST}/${NAME}.cpp ${SRC}/${NAME}.re
			MAIN_DEPENDENCY ${SRC}/${NAME}.re
		)
		#SET_SOURCE_FILES_PROPERTIES("${DST}/${NAME}.cpp" GENERATED)
		SET(LIBGRINGO_SRC ${LIBGRINGO_SRC} "${DST}/${NAME}.cpp")
	ELSE(NOT EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/generated/${NAME}.cpp")
		SET(LIBGRINGO_SRC ${LIBGRINGO_SRC} "${CMAKE_CURRENT_SOURCE_DIR}/generated/${NAME}.cpp")
	ENDIF(NOT EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/generated/${NAME}.cpp")
ENDMACRO(RE2C SRC DEST)

MACRO(LEMON NAME SRC DST)
	IF(NOT EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/generated/${NAME}.cpp" OR NOT EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/generated/${NAME}.h")
		# add lemon if needed
		IF(NOT TARGET_LEMON)
			ADD_EXECUTABLE(lemon src/lemon.c)
			SET(TARGET_LEMON TRUE)
		ENDIF(NOT TARGET_LEMON)
		ADD_CUSTOM_COMMAND(
			OUTPUT ${DST}/lempar.c
			COMMAND cp ${SRC}/lempar.c ${DST}/lempar.c
			MAIN_DEPENDENCY ${SRC}/lempar.c
		)
		ADD_CUSTOM_COMMAND(
			OUTPUT ${DST}/${NAME}.cpp ${DST}/${NAME}.h
			COMMAND ${EXECUTABLE_OUTPUT_PATH}/lemon -q ${SRC}/${NAME}.y
			COMMAND mv ${SRC}/${NAME}.c ${DST}/${NAME}.cpp
			COMMAND mv ${SRC}/${NAME}.h ${DST}/${NAME}.h
			COMMAND touch ${DST}/${NAME}.h ${DST}/${NAME}.cpp
			MAIN_DEPENDENCY ${SRC}/${NAME}.y
			DEPENDS lemon ${DST}/lempar.c
		)
		# add the .cpp file to the sources
		#SET_SOURCE_FILES_PROPERTIES("${DST}/${NAME}.cpp" GENERATED)
		SET(LIBGRINGO_SRC ${LIBGRINGO_SRC} "${DST}/${NAME}.cpp")
	ELSE(NOT EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/generated/${NAME}.cpp" OR NOT EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/generated/${NAME}.h")
		SET(LIBGRINGO_SRC ${LIBGRINGO_SRC} "${CMAKE_CURRENT_SOURCE_DIR}/generated/${NAME}.cpp")
	ENDIF(NOT EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/generated/${NAME}.cpp" OR NOT EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/generated/${NAME}.h")
ENDMACRO(LEMON NAME SRC DST)

RE2C(lparselexer ${CMAKE_CURRENT_SOURCE_DIR}/src ${CMAKE_CURRENT_BINARY_DIR})
LEMON(lparseparser_impl ${CMAKE_CURRENT_SOURCE_DIR}/src ${CMAKE_CURRENT_BINARY_DIR})

RE2C(plainlparselexer ${CMAKE_CURRENT_SOURCE_DIR}/src ${CMAKE_CURRENT_BINARY_DIR})
LEMON(lparseconverter_impl ${CMAKE_CURRENT_SOURCE_DIR}/src ${CMAKE_CURRENT_BINARY_DIR})

add_library(gringo-lib STATIC ${LIBGRINGO_SRC})
target_link_libraries (gringo-lib)
set_target_properties(gringo-lib PROPERTIES OUTPUT_NAME gringo)

CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/include/gringo.h.in ${CMAKE_CURRENT_BINARY_DIR}/config/gringo.h)

IF(PCHSupport_FOUND)
	add_precompiled_header(gringo-lib ${CMAKE_CURRENT_BINARY_DIR}/config/gringo.h)
ENDIF(PCHSupport_FOUND)



